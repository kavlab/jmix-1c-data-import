buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'io.jmix.gradle:jmix-gradle-plugin:2.7.0'
    }
}

group = 'ru.kavlab'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.jmix'

    repositories {
        mavenCentral()
        maven {
            url 'https://global.repo.jmix.io/repository/public'
        }
    }

    dependencies {
        implementation 'org.apache.olingo:odata-commons-api:4.9.0'
        implementation 'org.apache.olingo:odata-commons-core:4.9.0'
        implementation 'org.apache.olingo:odata-client-api:4.9.0'
        implementation 'org.apache.olingo:odata-client-core:4.9.0'
    }

    jmix {
        bomVersion = '2.7.0-RC2'
        projectId = 'imp1c'
    }

    group = this.group
    version = this.version

    def subArchivesBaseName = project.base.archivesName.get()
    def archName = subArchivesBaseName
    
    java {
        withSourcesJar()
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY"))
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            javaMaven(MavenPublication) {
                artifactId = archName
                from components.java
            }
        }
    }

    // remove after https://youtrack.jetbrains.com/issue/IDEA-227215 is fixed
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    project.tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add("-parameters")
    }
}
